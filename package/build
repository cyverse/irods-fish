#!/usr/bin/fish
#
# This script builds the Debian and Red Hat packages of irods-fish, placing them
# in the temporary sibling directory .build/. If .build/ exists when the script
# is run, the directory is first deleted to ensure the packages contain no
# accidental artifacts.


function main
	set pkgDir (dirname (realpath (status --current-filename)))
	set baseDir (dirname $pkgDir)

	set buildDir $baseDir/.build
	rm --force --recursive $buildDir
	mkdir --parents $buildDir

	set stagingDir $buildDir/irods-fish
	mkdir --parents $stagingDir

	set cfgDir $stagingDir/etc/fish
	mkdir --parents $cfgDir/completions $cfgDir/functions

	set envDir $baseDir/environment
	cp $envDir/completions/functions/* $envDir/functions/* $envDir/prompt/* $cfgDir/functions/
	cp $envDir/completions/*.fish $cfgDir/completions/

  set portDir $baseDir/irods_completion-orig
	cp $portDir/functions/* $cfgDir/functions/
  cp $portDir/completions/* $cfgDir/completions/

	mkdir --parents $stagingDir/DEBIAN
	cp $pkgDir/control $stagingDir/DEBIAN/

	set dpkgResp (dpkg-deb --build $stagingDir $baseDir)
	set debPkg (string replace --filter --regex '.* in \'(.*)\'\.' '$1' $dpkgResp)

	if test $debPkg = ''
		 or not fpm --force --log error --input-type deb --output-type rpm $debPkg > /dev/null 2>&1

		printf 'Failed to generate the RPM package\n' >&2
		return 1
	end

	rm --force --recursive $buildDir
end


main
